
#-----------------------------------------------------------
# Baseline generating run

if (${ICE_BASEGEN} != ${ICE_SPVAL}) then
  
  set baseline_dir = ${ICE_BASELINE}/${ICE_BASEGEN}/${ICE_TESTNAME}

  if (-e ${baseline_dir}) then
    echo "FAIL ${ICE_TESTNAME} generate baseline-already-exists" >> ${ICE_CASEDIR}/test_output
    echo "Baseline directory (${baseline_dir}) already exists. Exiting..."
  else
    mkdir -p ${baseline_dir}
    cp -p -r ${ICE_RUNDIR}/* ${baseline_dir}/
    echo "PASS ${ICE_TESTNAME} generate ${ICE_BASEGEN}" >> ${ICE_CASEDIR}/test_output
  endif
endif

#-----------------------------------------------------------
# Baseline comparing run

if (${ICE_BASECOM} != ${ICE_SPVAL}) then
  set btimeloop = -1
  set bdynamics = -1
  set bcolumn = -1

  if (${ICE_TEST} == "unittest") then
    set test_file = `ls -1t ${ICE_RUNDIR}/cice.runlog* | head -1`
    set base_file = `ls -1t ${ICE_BASELINE}/${ICE_BASECOM}/${ICE_TESTNAME}/cice.runlog* | head -1`

    echo ""
    echo "Regression Log Compare Mode:"
    echo "base_file: ${base_file}"
    echo "test_file: ${test_file}"

    if (${ICE_TARGET} == "opticep") then
       ${ICE_CASEDIR}/casescripts/comparelog.csh ${base_file} ${test_file} modcicefile
       set bfbstatus = $status
    else
       ${ICE_CASEDIR}/casescripts/comparelog.csh ${base_file} ${test_file} notcicefile
       set bfbstatus = $status
    endif

  else if (${ICE_BFBTYPE} =~ qcchk*) then
    set test_dir = ${ICE_RUNDIR}
    set base_dir  = ${ICE_BASELINE}/${ICE_BASECOM}/${ICE_TESTNAME}
    ${ICE_SANDBOX}/configuration/scripts/tests/QC/cice.t-test.py ${base_dir} ${test_dir}
    set bfbstatus = $status

  else

    set test_dir = ${ICE_RUNDIR}/restart
    set base_dir  = ${ICE_BASELINE}/${ICE_BASECOM}/${ICE_TESTNAME}/restart

    set baseline_log = `ls -1t ${ICE_BASELINE}/${ICE_BASECOM}/${ICE_TESTNAME}/cice.runlog* | head -1`
    if (${baseline_log} != "" ) then
      set btimeloop = `grep TimeLoop ${baseline_log} | grep Timer | cut -c 22-32`
      set bdynamics = `grep Dynamics ${baseline_log} | grep Timer | cut -c 22-32`
      set bcolumn   = `grep Column   ${baseline_log} | grep Timer | cut -c 22-32`
      if (${btimeloop} == "") set btimeloop = -1
      if (${bdynamics} == "") set bdynamics = -1
      if (${bcolumn}   == "") set bcolumn = -1
    endif

    echo ""
    echo "Regression Restart Compare Mode:"
    echo "base_dir: ${base_dir}"
    echo "test_dir: ${test_dir}"

    ${ICE_CASEDIR}/casescripts/comparebfb.csh ${base_dir} ${test_dir}
    set bfbstatus = $status

    if ( ${bfbstatus} != 0 ) then

      set test_file = `ls -1t ${ICE_RUNDIR}/cice.runlog* | head -1`
      set base_file = `ls -1t ${ICE_BASELINE}/${ICE_BASECOM}/${ICE_TESTNAME}/cice.runlog* | head -1`

      echo ""
      echo "Regression Log Compare Mode:"
      echo "base_file: ${base_file}"
      echo "test_file: ${test_file}"

      if ("${base_file}" == "" || "${test_file}" == "" ) then
        echo "MISS ${ICE_TESTNAME} complog ${ICE_BASECOM} missing-data" >> ${ICE_CASEDIR}/test_output
        echo "Missing data"
      else
        ${ICE_CASEDIR}/casescripts/comparelog.csh ${base_file} ${test_file}
        set logstatus = $status

        if ( ${logstatus} == 0 ) then
          echo "PASS ${ICE_TESTNAME} complog ${ICE_BASECOM}" >> ${ICE_CASEDIR}/test_output
          echo "Regression baseline and test dataset may be the same"
        else if ( ${logstatus} == 1 ) then
          echo "FAIL ${ICE_TESTNAME} complog ${ICE_BASECOM} different-data" >> ${ICE_CASEDIR}/test_output
          echo "Regression baseline and test dataset are not the same"
        else if ( ${logstatus} == 2 ) then
          echo "MISS ${ICE_TESTNAME} complog ${ICE_BASECOM} missing-data" >> ${ICE_CASEDIR}/test_output
          echo "Missing data"
        else
          echo "FAIL ${ICE_TESTNAME} complog ${ICE_BASECOM} usage-error" >> ${ICE_CASEDIR}/test_output
          echo "Regression baseline and test dataset error in usage"
        endif
      endif

    endif

  endif

  if ( ${bfbstatus} == 0 ) then
    echo "PASS ${ICE_TESTNAME} compare ${ICE_BASECOM} ${btimeloop} ${bdynamics} ${bcolumn}" >> ${ICE_CASEDIR}/test_output
    echo "Regression baseline and test dataset are identical"
  else if ( ${bfbstatus} == 1 ) then
    echo "FAIL ${ICE_TESTNAME} compare ${ICE_BASECOM} ${btimeloop} ${bdynamics} ${bcolumn} different-data" >> ${ICE_CASEDIR}/test_output
    echo "Regression baseline and test dataset are different"
  else if ( ${bfbstatus} == 2 ) then
    echo "MISS ${ICE_TESTNAME} compare ${ICE_BASECOM}  ${btimeloop} ${bdynamics} ${bcolumn} missing-data" >> ${ICE_CASEDIR}/test_output
    echo "Missing data"
  else
    echo "FAIL ${ICE_TESTNAME} compare ${ICE_BASECOM} ${btimeloop} ${bdynamics} ${bcolumn} usage-error" >> ${ICE_CASEDIR}/test_output
    echo "Regression baseline and test dataset error in usage"
  endif
endif

#-----------------------------------------------------------

# BFB Compare runs

if (${ICE_BFBCOMP} != ${ICE_SPVAL}) then

  echo "PEND ${ICE_TESTNAME} bfbcomp ${ICE_BFBCOMP}" >> ${ICE_CASEDIR}/test_output
  if (${ICE_BFBCOMP} != ${ICE_TESTNAME}) then
    # Check if the baseline job is complete
    set job = `grep " ${ICE_BFBCOMP} " ../suite.jobs | sed  's|^[^0-9]*\([0-9]*\).*$|\1|g'`
    echo "checking on Job $job"
    set qstatjob = 1
    set cnt = 0
    if (${job} =~ [0-9]*) then
      while ($qstatjob)
        set qstatus = `${ICE_MACHINE_QSTAT} $job | grep $job | wc -l`
#        ${ICE_MACHINE_QSTAT} $job
#        echo $job $qstatus
        if ($qstatus == 0) then
          echo "Job $job completed"
          set qstatjob = 0
        else
          @ cnt = $cnt + 1
          echo "Waiting for $job to complete $cnt"
          sleep 60   # Sleep for 1 minute, so as not to overwhelm the queue manager
          if ($cnt > 5) then
            echo "No longer waiting for $job to complete"
            set qstatjob = 0 # Abandon check after cnt sleep 60 checks
          endif
        endif
#        echo $qstatjob
      end
    endif
  endif

  if (${ICE_BFBTYPE} == "log") then
    set test_file = `ls -1t ${ICE_RUNDIR}/cice.runlog* | head -1`
    set base_file = `ls -1t ${ICE_RUNDIR}/../${ICE_BFBCOMP}.${ICE_TESTID}/cice.runlog* | head -1`

    echo ""
    echo "bfb Log Compare Mode:"
    echo "base_file: ${base_file}"
    echo "test_file: ${test_file}"

    if (${ICE_TARGET} == "opticep") then
       ${ICE_CASEDIR}/casescripts/comparelog.csh ${base_file} ${test_file} modcicefile
       set bfbstatus = $status
    else
       ${ICE_CASEDIR}/casescripts/comparelog.csh ${base_file} ${test_file}
       set bfbstatus = $status
    endif

  else if (${ICE_BFBTYPE} == "logrest") then
    set test_file = `ls -1t ${ICE_RUNDIR}/cice.runlog* | head -1`
    set base_file = `ls -1t ${ICE_RUNDIR}/../${ICE_BFBCOMP}.${ICE_TESTID}/cice.runlog* | head -1`

    echo ""
    echo "bfb Log Compare Mode:"
    echo "base_file: ${base_file}"
    echo "test_file: ${test_file}"

    if (${ICE_TARGET} == "opticep") then
       ${ICE_CASEDIR}/casescripts/comparelog.csh ${base_file} ${test_file} modcicefile
       set bfbstatus = $status
    else
       ${ICE_CASEDIR}/casescripts/comparelog.csh ${base_file} ${test_file}
       set bfbstatusl = $status
    endif

    set test_dir = ${ICE_RUNDIR}/restart
    set base_dir = ${ICE_RUNDIR}/../${ICE_BFBCOMP}.${ICE_TESTID}/restart

    echo ""
    echo "bfb Restart Compare Mode:"
    echo "base_dir: ${base_dir}"
    echo "test_dir: ${test_dir}"

    ${ICE_CASEDIR}/casescripts/comparebfb.csh ${base_dir} ${test_dir}
    set bfbstatusr = $status

    if ({$bfbstatusl} == ${bfbstatusr}) then
       set bfbstatus = ${bfbstatusl}
    else if (${bfbstatusl} == 1 || ${bfbstatusr} == 1) then
       set bfbstatus = 1
    else if ({$bfbstatusl} > ${bfbstatusr}) then
       set bfbstatus = ${bfbstatusl}
    else
       set bfbstatus = ${bfbstatusr}
    endif

    echo "bfb log, rest, combined status = ${bfbstatusl},${bfbstatusr},${bfbstatus}"

  else if (${ICE_BFBTYPE} =~ qcchk*) then
    set test_dir = ${ICE_RUNDIR}
    set base_dir = ${ICE_RUNDIR}/../${ICE_BFBCOMP}.${ICE_TESTID}
    echo ""
    echo "qcchk Compare Mode:"
    echo "base_dir: ${base_dir}"
    echo "test_dir: ${test_dir}"
    ${ICE_SANDBOX}/configuration/scripts/tests/QC/cice.t-test.py ${base_dir} ${test_dir}
    set bfbstatus = $status
    # expecting failure, so switch value
    if (${ICE_BFBTYPE} == "qcchkf") then
       @ bfbstatus = 1 - $bfbstatus
    endif

  else
    set test_dir = ${ICE_RUNDIR}/restart
    set base_dir = ${ICE_RUNDIR}/../${ICE_BFBCOMP}.${ICE_TESTID}/restart

    echo ""
    echo "bfb Restart Compare Mode:"
    echo "base_dir: ${base_dir}"
    echo "test_dir: ${test_dir}"

    ${ICE_CASEDIR}/casescripts/comparebfb.csh ${base_dir} ${test_dir}
    set bfbstatus = $status
  endif

  mv -f ${ICE_CASEDIR}/test_output ${ICE_CASEDIR}/test_output.prev
  cat ${ICE_CASEDIR}/test_output.prev | grep -iv "${ICE_TESTNAME} bfbcomp " >! ${ICE_CASEDIR}/test_output
  rm -f ${ICE_CASEDIR}/test_output.prev
  if (${bfbstatus} == 0) then
    echo "PASS ${ICE_TESTNAME} bfbcomp ${ICE_BFBCOMP}" >> ${ICE_CASEDIR}/test_output
    echo "bfbcomp baseline and test dataset pass"
  else if (${bfbstatus} == 1) then
    echo "FAIL ${ICE_TESTNAME} bfbcomp ${ICE_BFBCOMP} different-data" >> ${ICE_CASEDIR}/test_output
    echo "bfbcomp baseline and test dataset fail"
  else if (${bfbstatus} == 2) then
    echo "MISS ${ICE_TESTNAME} bfbcomp ${ICE_BFBCOMP} missing-data" >> ${ICE_CASEDIR}/test_output
    echo "Missing data"
  else
    echo "FAIL ${ICE_TESTNAME} bfbcomp ${ICE_BFBCOMP} usage-error" >> ${ICE_CASEDIR}/test_output
    echo "bfbcomp and test dataset usage error"
  endif
endif

#-----------------------------------------------------------

# Document machine and env

set machinfo = ""
set envinfo  = ""
if ($?ICE_MACHINE_MACHINFO) then
  set machinfo = "${ICE_MACHINE_MACHINFO}"
endif
if ($?ICE_MACHINE_ENVINFO) then
  set envinfo = "${ICE_MACHINE_ENVINFO}"
endif
echo "#machinfo = ${machinfo}" >> ${ICE_CASEDIR}/test_output
echo "#envinfo ${ICE_ENVNAME} = ${envinfo}" >> ${ICE_CASEDIR}/test_output

#-----------------------------------------------------------
