
# Build around a 10 day run with restart at day 5.
#-----------------------------------------------------------
# Run the CICE model baseline simulation

cp ice_in ice_in.0
${ICE_CASEDIR}/casescripts/parse_namelist.sh ice_in ${ICE_CASEDIR}/casescripts/test_nml.restart1
cp ice_in ice_in.1

./cice.run
set res="$status"

if ( $res != 0 ) then
  mv -f ${ICE_CASEDIR}/test_output ${ICE_CASEDIR}/test_output.prev
  cat ${ICE_CASEDIR}/test_output.prev | grep -iv "${ICE_TESTNAME} run" >! ${ICE_CASEDIR}/test_output
  mv -f ${ICE_CASEDIR}/test_output ${ICE_CASEDIR}/test_output.prev
  cat ${ICE_CASEDIR}/test_output.prev | grep -iv "${ICE_TESTNAME} test " >! ${ICE_CASEDIR}/test_output
  rm -f ${ICE_CASEDIR}/test_output.prev
  echo "FAIL ${ICE_TESTNAME} run" >> ${ICE_CASEDIR}/test_output
  echo "FAIL ${ICE_TESTNAME} test " >> ${ICE_CASEDIR}/test_output
  exit 99
endif

# Compute date of last file in first run
if ( "${ICE_IOTYPE}" == "binary" ) then
  set end_date = `ls -t1 ${ICE_RUNDIR}/restart | head -1 | rev | cut -d "." -f 1 | rev`
else
  set end_date = `ls -t1 ${ICE_RUNDIR}/restart | head -1 | rev | cut -d "." -f 2 | rev`
endif

# Prepend 'base_' to final restart and history restart files to save for comparison
foreach file (${ICE_RUNDIR}/restart/*${end_date}*)
  set surname = `echo $file | awk -F'/' '{print $NF}'`
  mv $file ${ICE_RUNDIR}/restart/base_$surname
end

# Prepend 'base_' to all history files to save for comparison
foreach file (${ICE_RUNDIR}/history/*)
  set surname = `echo $file | awk -F'/' '{print $NF}'`
  mv $file ${ICE_RUNDIR}/history/base_$surname
end

#-----------------------------------------------------------
# Run the CICE model for the restart simulation

# Modify the contents of the pointer file for restart
perl -i -pe's/(\d{4})-(\d{2})-(\d{2})/sprintf("%04d-%02d-%02d",$1,$2,$3-5)/e' ${ICE_RUNDIR}/ice.restart_file

${ICE_CASEDIR}/casescripts/parse_namelist.sh ice_in ${ICE_CASEDIR}/casescripts/test_nml.restart2
cp ice_in ice_in.2

./cice.run
set res="$status"

cp ice_in.0 ice_in

mv -f ${ICE_CASEDIR}/test_output ${ICE_CASEDIR}/test_output.prev
cat ${ICE_CASEDIR}/test_output.prev | grep -iv "${ICE_TESTNAME} run" >! ${ICE_CASEDIR}/test_output
mv -f ${ICE_CASEDIR}/test_output ${ICE_CASEDIR}/test_output.prev
cat ${ICE_CASEDIR}/test_output.prev | grep -iv "${ICE_TESTNAME} test" >! ${ICE_CASEDIR}/test_output
rm -f ${ICE_CASEDIR}/test_output.prev

if ( $res != 0 ) then
  echo "FAIL ${ICE_TESTNAME} run " >> ${ICE_CASEDIR}/test_output
  echo "FAIL ${ICE_TESTNAME} test " >> ${ICE_CASEDIR}/test_output
  exit 99
else
  set log_file = `ls -t1 ${ICE_RUNDIR}/cice.runlog* | head -1`
  set ttimeloop = `grep TimeLoop ${log_file} | grep Timer | cut -c 22-32`
  set tdynamics = `grep Dynamics ${log_file} | grep Timer | cut -c 22-32`
  set tcolumn   = `grep Column   ${log_file} | grep Timer | cut -c 22-32`
  if (${ttimeloop} == "") set ttimeloop = -1
  if (${tdynamics} == "") set tdynamics = -1
  if (${tcolumn}   == "") set tcolumn = -1
  echo "PASS ${ICE_TESTNAME} run ${ttimeloop} ${tdynamics} ${tcolumn}" >> ${ICE_CASEDIR}/test_output

  set restbfb = "PASS"
  ${ICE_CASEDIR}/casescripts/comparebfb.csh ${ICE_RUNDIR}/restart
  set bfbstatus = $status
  if (${bfbstatus} != 0) set restbfb = "FAIL"

  set icehrbfb = "NONE"
  if (${ICE_TESTNAME} =~ *histall10d* && ${ICE_IOTYPE} !~ *binary* ) then
    echo "${restbfb} ${ICE_TESTNAME} restart " >> ${ICE_CASEDIR}/test_output
    foreach file1 (${ICE_RUNDIR}/restart/base_iceh* ${ICE_RUNDIR}/history/base_iceh*)
      set file2  = `echo $file1 | sed -e 's|base_iceh|iceh|g'`
      if (-e ${file2} && `where ncatted` != "") then
        if (${icehrbfb} == "NONE") set icehrbfb = "PASS"
        # remove global attributes to do bfb binary compare
        set file1m = `echo $file1 | sed -e 's|base_iceh|base_iceh_mod|g'`
        set file2m = `echo $file1 | sed -e 's|base_iceh|iceh_mod|g'`
        ncatted -h -a ,global,d,, ${file1} ${file1m}
        ncatted -h -a ,global,d,, ${file2} ${file2m}
        ${ICE_CASEDIR}/casescripts/comparebfb.csh ${file1m} ${file2m}
        set bfbstatus = $status
        if (${bfbstatus} != 0) set icehrbfb = "FAIL"
        rm ${file1m} ${file2m}
      endif
    end
    echo "${icehrbfb} ${ICE_TESTNAME} histrest " >> ${ICE_CASEDIR}/test_output
  endif

  if (${restbfb} == PASS && ${icehrbfb} == PASS) then
    echo "PASS ${ICE_TESTNAME} test " >> ${ICE_CASEDIR}/test_output
  else if (${restbfb} == PASS && ${icehrbfb} == NONE) then
    echo "PASS ${ICE_TESTNAME} test " >> ${ICE_CASEDIR}/test_output
  else
    echo "FAIL ${ICE_TESTNAME} test " >> ${ICE_CASEDIR}/test_output
  endif

endif

#-----------------------------------------------------------

