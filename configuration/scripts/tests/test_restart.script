
# Build around a 10 day run with restart at day 5.
#-----------------------------------------------------------
# Run the CICE model baseline simulation

cp ice_in ice_in.0
${ICE_CASEDIR}/casescripts/parse_namelist.sh ice_in ${ICE_CASEDIR}/casescripts/test_nml.restart1
cp ice_in ice_in.1

mv -f ${ICE_CASEDIR}/test_output ${ICE_CASEDIR}/test_output.prev
cat ${ICE_CASEDIR}/test_output.prev | grep -iv "${ICE_TESTNAME} run" >! ${ICE_CASEDIR}/test_output
mv -f ${ICE_CASEDIR}/test_output ${ICE_CASEDIR}/test_output.prev
cat ${ICE_CASEDIR}/test_output.prev | grep -iv "${ICE_TESTNAME} test" >! ${ICE_CASEDIR}/test_output
rm -f ${ICE_CASEDIR}/test_output.prev
echo "RUN  ${ICE_TESTNAME} run " >> ${ICE_CASEDIR}/test_output
echo "PEND ${ICE_TESTNAME} test " >> ${ICE_CASEDIR}/test_output

./cice.run
set res="$?"

if ( $res != 0 ) then
  mv -f ${ICE_CASEDIR}/test_output ${ICE_CASEDIR}/test_output.prev
  cat ${ICE_CASEDIR}/test_output.prev | grep -iv "${ICE_TESTNAME} run" >! ${ICE_CASEDIR}/test_output
  mv -f ${ICE_CASEDIR}/test_output ${ICE_CASEDIR}/test_output.prev
  cat ${ICE_CASEDIR}/test_output.prev | grep -iv "${ICE_TESTNAME} test " >! ${ICE_CASEDIR}/test_output
  rm -f ${ICE_CASEDIR}/test_output.prev
  echo "FAIL ${ICE_TESTNAME} run" >> ${ICE_CASEDIR}/test_output
  echo "FAIL ${ICE_TESTNAME} test " >> ${ICE_CASEDIR}/test_output
  exit 99
else
  echo "PASS ${ICE_TESTNAME} initialrun" >> ${ICE_CASEDIR}/test_output
endif

# Prepend 'baseline_' to the final restart file to save for comparison
if ( "${ICE_IOTYPE}" == "binary" ) then
  set end_date = `ls -t1 ${ICE_RUNDIR}/restart | head -1 | awk -F'.' '{print $NF}'`
  foreach file (${ICE_RUNDIR}/restart/*${end_date})
    set surname = `echo $file | awk -F'/' '{print $NF}'`
    mv $file ${ICE_RUNDIR}/restart/base_$surname
  end
else
  set test_file = `ls -t1 ${ICE_RUNDIR}/restart | head -1`
  set test_data = ${ICE_RUNDIR}/restart/${test_file}
  set base_data = ${ICE_RUNDIR}/restart/base_${test_file}
  mv ${test_data} ${base_data}
endif

#-----------------------------------------------------------
# Run the CICE model for the restart simulation

# Modify the contents of the pointer file for restart
perl -i -pe's/(\d{4})-(\d{2})-(\d{2})/sprintf("%04d-%02d-%02d",$1,$2,$3-5)/e' ${ICE_RUNDIR}/ice.restart_file

${ICE_CASEDIR}/casescripts/parse_namelist.sh ice_in ${ICE_CASEDIR}/casescripts/test_nml.restart2
cp ice_in ice_in.2

./cice.run
set res="$?"

cp ice_in.0 ice_in

mv -f ${ICE_CASEDIR}/test_output ${ICE_CASEDIR}/test_output.prev
cat ${ICE_CASEDIR}/test_output.prev | grep -iv "${ICE_TESTNAME} run" >! ${ICE_CASEDIR}/test_output
mv -f ${ICE_CASEDIR}/test_output ${ICE_CASEDIR}/test_output.prev
cat ${ICE_CASEDIR}/test_output.prev | grep -iv "${ICE_TESTNAME} test" >! ${ICE_CASEDIR}/test_output
rm -f ${ICE_CASEDIR}/test_output.prev

if ( $res != 0 ) then
  echo "FAIL ${ICE_TESTNAME} run " >> ${ICE_CASEDIR}/test_output
  echo "FAIL ${ICE_TESTNAME} test " >> ${ICE_CASEDIR}/test_output
  exit 99
else
  set log_file = `ls -t1 ${ICE_RUNDIR}/cice.runlog* | head -1`
  set ttimeloop = `grep TimeLoop ${log_file} | grep Timer | cut -c 22-32`
  set tdynamics = `grep Dynamics ${log_file} | grep Timer | cut -c 22-32`
  set tcolumn   = `grep Column   ${log_file} | grep Timer | cut -c 22-32`
  if (${ttimeloop} == "") set ttimeloop = -1
  if (${tdynamics} == "") set tdynamics = -1
  if (${tcolumn}   == "") set tcolumn = -1
  echo "PASS ${ICE_TESTNAME} run ${ttimeloop} ${tdynamics} ${tcolumn}" >> ${ICE_CASEDIR}/test_output

  echo "Exact Restart Comparison Mode:"
  if ( "${ICE_IOTYPE}" == "binary" ) then
    set end_date = `ls -t1 ${ICE_RUNDIR}/restart | head -1 | awk -F'.' '{print $NF}'`
    set failure = 0
    foreach file (${ICE_RUNDIR}/restart/base_*${end_date})
      echo "Performing binary comparison between files:"
      set base_data = `echo $file | awk -F'/' '{print $NF}'`
      set test_data = `echo $file | awk -F'/' '{print $NF}' | cut -c6-`
      echo "base: $base_data"
      echo "test: $test_data"
      cmp -s $base_data $test_data
      if ( $? == 1 ) then
        set failure = 1
        echo "Failure in data comparison"
        break
      endif
    end
    if ( $failure == 0 ) then
      echo "PASS ${ICE_TESTNAME} test " >> ${ICE_CASEDIR}/test_output
    else
      echo "FAIL ${ICE_TESTNAME} test " >> ${ICE_CASEDIR}/test_output
    endif
  else
    echo "Performing binary comparison between files:"
    echo "base: $base_data"
    echo "test: $test_data"
    if ( { cmp -s $test_data $base_data } ) then
      echo "PASS ${ICE_TESTNAME} test " >> ${ICE_CASEDIR}/test_output
    else
      echo "FAIL ${ICE_TESTNAME} test " >> ${ICE_CASEDIR}/test_output
    endif
  endif

  set base_log = `ls -t1 ${ICE_CASEDIR}/logs/cice.runlog* | head -2 | tail -1`
  set test_log = `ls -t1 ${ICE_CASEDIR}/logs/cice.runlog* | head -1`
  set base_out = `awk "BEGIN{found=0;minmax=0;} /$test_data/{found=1} {if (found) if(/min, max, sum =/) minmax=1; else minmax=0; if(minmax) print }" $base_log`
  set test_out = `awk "BEGIN{found=0;minmax=0;} /$test_data/{found=1} {if (found) if(/min, max, sum =/) minmax=1; else minmax=0; if(minmax) print }" $test_log`
  echo ""
  echo ""
  echo "Performing comparison between log files:"
  echo "base: $base_log"
  echo "test: $test_log"
  if ( "$base_out" == "$test_out" ) then
    echo "PASS ${ICE_TESTNAME} test_log " >> ${ICE_CASEDIR}/test_output
  else
    echo "FAIL ${ICE_TESTNAME} test_log " >> ${ICE_CASEDIR}/test_output
  endif
  
endif

#-----------------------------------------------------------

