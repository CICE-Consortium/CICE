
# Build and run case with different decompositions
#-----------------------------------------------------------
# Run the CICE model baseline simulation

set decomps = "squarepop squareice slenderX2 slenderX1 roundrobin sectcart sectrobin spacecurve rakeX2 rakeX1 rakepop rakeice"

mv -f ${ICE_CASEDIR}/test_output ${ICE_CASEDIR}/test_output.prev
cat ${ICE_CASEDIR}/test_output.prev | grep -iv "${ICE_TESTNAME} build" >! ${ICE_CASEDIR}/test_output
mv -f ${ICE_CASEDIR}/test_output ${ICE_CASEDIR}/test_output.prev
cat ${ICE_CASEDIR}/test_output.prev | grep -iv "${ICE_TESTNAME} run" >! ${ICE_CASEDIR}/test_output
rm -f ${ICE_CASEDIR}/test_output.prev

foreach decomp (${decomps})
  echo "PASS ${ICE_TESTNAME}_${decomp} build" >> ${ICE_CASEDIR}/test_output
  echo "PEND ${ICE_TESTNAME}_${decomp} run" >> ${ICE_CASEDIR}/test_output
end

cp ice_in ice_in.0
set base_data = ""

foreach decomp (${decomps})
  ${ICE_CASEDIR}/casescripts/parse_namelist.sh ice_in ${ICE_CASEDIR}/casescripts/set_nml.d${decomp}
  cp ice_in ice_in.${decomp}

  ./cice.run
  set res="$?"

  set grade = FAIL
  if ( $res == 0 ) then
    set grade = PASS
  endif

  mv -f ${ICE_CASEDIR}/test_output ${ICE_CASEDIR}/test_output.prev
  cat ${ICE_CASEDIR}/test_output.prev | grep -iv "${ICE_TESTNAME}_${decomp}" >! ${ICE_CASEDIR}/test_output
  rm -f ${ICE_CASEDIR}/test_output.prev

  # bfbcomp for this test
  if (${grade} != PASS) then
    echo "$grade ${ICE_TESTNAME}_${decomp} run" >> ${ICE_CASEDIR}/test_output
    echo "$grade ${ICE_TESTNAME}_${decomp} test" >> ${ICE_CASEDIR}/test_output
  else

    set log_file = `ls -t1 ${ICE_RUNDIR}/cice.runlog* | head -1`
    mv -f ${log_file} ${log_file}.${decomp}
    set log_file = ${log_file}.${decomp}
    set ttimeloop = `grep TimeLoop ${log_file} | grep Timer | cut -c 22-32`
    set tdynamics = `grep Dynamics ${log_file} | grep Timer | cut -c 22-32`
    set tcolumn   = `grep Column   ${log_file} | grep Timer | cut -c 22-32`
    if (${ttimeloop} == "") set ttimeloop = -1
    if (${tdynamics} == "") set tdynamics = -1
    if (${tcolumn}   == "") set tcolumn = -1

    set test_file = `ls -t1 ${ICE_RUNDIR}/restart | head -1`
    set test_data = ${ICE_RUNDIR}/restart/${test_file}
    mv -f ${test_data} ${test_data}.${decomp}
    set test_file = ${test_file}.${decomp}
    set test_data = ${test_data}.${decomp}

    echo "$grade ${ICE_TESTNAME}_${decomp} run ${ttimeloop} ${tdynamics} ${tcolumn}" >> ${ICE_CASEDIR}/test_output
    echo "$grade ${ICE_TESTNAME}_${decomp} test" >> ${ICE_CASEDIR}/test_output

    # bfb compare section
    if (${base_data} == "") then
      # First run is the base data
      set base_case = ${ICE_TESTNAME}_${decomp}
      set base_data = ${test_data}
    else
      set grade = FAIL
      if ( { cmp -s $test_data $base_data } ) then
        set grade = PASS
      endif
      echo "$grade ${ICE_TESTNAME}_${decomp} bfbcomp ${base_case}" >> ${ICE_CASEDIR}/test_output
    endif

    # compare section
    if (${ICE_BASECOM} != ${ICE_SPVAL}) then

      set baseline_dir  = ${ICE_BASELINE}/${ICE_BASECOM}/${ICE_TESTNAME}/restart
      set baseline_data = ${baseline_dir}/${test_file}

      set baseline_log = `ls -1t ${ICE_BASELINE}/${ICE_BASECOM}/${ICE_TESTNAME}/cice.runlog*.${decomp} | head -1`
      set btimeloop = -1
      set bdynamics = -1
      set bcolumn = -1
      if (${baseline_log} != "" ) then
        set btimeloop = `grep TimeLoop ${baseline_log} | grep Timer | cut -c 22-32`
        set bdynamics = `grep Dynamics ${baseline_log} | grep Timer | cut -c 22-32`
        set bcolumn   = `grep Column   ${baseline_log} | grep Timer | cut -c 22-32`
        if (${btimeloop} == "") set btimeloop = -1
        if (${bdynamics} == "") set bdynamics = -1
        if (${bcolumn}   == "") set bcolumn = -1
      endif

      echo ""
      echo "Regression Compare Mode:"
      echo "Performing binary comparison between files"
      echo "baseline: ${baseline_data}"
      echo "test:     ${test_data}"
      if (-e ${baseline_data} ) then
        if ( { cmp -s ${test_data} ${baseline_data} } ) then
          echo "PASS ${ICE_TESTNAME}_$decomp compare ${ICE_BASECOM} ${btimeloop} ${bdynamics} ${bcolumn}" >> ${ICE_CASEDIR}/test_output
        else
          echo "FAIL ${ICE_TESTNAME}_$decomp compare ${ICE_BASECOM} ${btimeloop} ${bdynamics} ${bcolumn} different-data" >> ${ICE_CASEDIR}/test_output
          echo "Regression baseline and test dataset are different"
        endif
      else
        echo "MISS ${ICE_TESTNAME}_$decomp compare ${ICE_BASECOM}  ${btimeloop} ${bdynamics} ${bcolumn} missing-data" >> ${ICE_CASEDIR}/test_output
        echo "Missing data"
      endif
    endif
  endif

end

cp ice_in.0 ice_in
#-----------------------------------------------------------

# turn off general test features, these are done above for this test
setenv ICE_BASECOM ${ICE_SPVAL}
setenv ICE_BFBCOMP ${ICE_SPVAL}

#-----------------------------------------------------------
